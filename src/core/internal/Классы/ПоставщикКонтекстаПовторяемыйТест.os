#Использовать collectionos
#Использовать annotations

Перем _Поделка;

Функция ПредоставитьКонтекстыВызова(КонтекстРасширения) Экспорт

	ОтображаемоеИмя = КонтекстРасширения.ОтображаемоеИмя();
	ТестМетод       = КонтекстРасширения.ТестМетод();

	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТестМетод, "ПовторяемыйТест");
	КоличествоПовторений = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Значение");
	ШаблонИмени = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
		Аннотация,
		"Имя",
		"Повторение {ТекущееПовторение} из {ВсегоПовторений}"
	);

	ШаблонИмени = СтрЗаменить(ШаблонИмени, "{ОтображаемоеИмя}", ОтображаемоеИмя);
	ШаблонИмени = СтрЗаменить(ШаблонИмени, "{ВсегоПовторений}", КоличествоПовторений);

	Описания = Новый СписокМассив;

	Для Повторение = 1 По КоличествоПовторений Цикл

		Имя = СтрЗаменить(ШаблонИмени, "{ТекущееПовторение}", Повторение);

		Описания.Добавить(
			_Поделка.НайтиЖелудь(
				"ОписаниеТеста",
				Массивы.ИзЭлементов(
					Имя,
					Новый СписокМассив
				)
			)
		);

	КонецЦикла;

	Возврат Описания.ПроцессорКоллекции();

КонецФункции

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин Поделка)
	_Поделка = Поделка;
КонецПроцедуры
