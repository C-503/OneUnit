Перем _ПутьКОтчету;

Перем ЗаписьXML;
Перем ВремяНачалаТеста;

&ПодпискаНаСобытие("ИсполнениеТестПланНачало")
Процедура ИсполнениеТестПланНачало(ТестПлан) Экспорт

	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(_ПутьКОтчету);

	ЗаписьXML.ЗаписатьНачалоЭлемента("testExecutions");
	ЗаписьXML.ЗаписатьАтрибут("version", 1);

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПланКонец")
Процедура ИсполнениеТестПланКонец(ТестПлан) Экспорт

	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборНачало")
Процедура ИсполнениеТестНаборНачало(ТестНабор) Экспорт

	ЗаписьXML.ЗаписатьНачалоЭлемента("file");
	ЗаписьXML.ЗаписатьАтрибут("path", ТестНабор.Путь());

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборКонец")
Процедура ИсполнениеТестНаборКонец(ТестНабор, Результат) Экспорт

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборПропущен")
Процедура ИсполнениеТестНаборПропущен(ТестНабор, Причина) Экспорт

	ЗаписьXML.ЗаписатьНачалоЭлемента("file");
	ЗаписьXML.ЗаписатьАтрибут("path", ТестНабор.Путь());

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНачало")
Процедура ИсполнениеТестНачало(Тест) Экспорт
	ВремяНачалаТеста = ТекущаяУниверсальнаяДатаВМиллисекундах();
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестКонец")
Процедура ИсполнениеТестКонец(Тест, Результат) Экспорт

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Контейнер Тогда
		Возврат;
	КонецЕсли;

	ЗаписьXML.ЗаписатьНачалоЭлемента("testCase");
	ЗаписьXML.ЗаписатьАтрибут("name", Тест.ПолноеИмя());
	ЗаписьXML.ЗаписатьАтрибут("duration", ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяНачалаТеста);

	Если Результат.Результат() = РезультатыВыполненияТестов.Ошибка Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("failure");
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПропущен")
Процедура ИсполнениеТестПропущен(Тест, Причина) Экспорт

	ЗаписьXML.ЗаписатьНачалоЭлемента("testCase");
	ЗаписьXML.ЗаписатьАтрибут("name", Тест.ПолноеИмя());
	ЗаписьXML.ЗаписатьАтрибут("duration", 0);

	ЗаписьXML.ЗаписатьНачалоЭлемента("skipped");
	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьXML.ЗаписатьКонецЭлемента();

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(&Блестяшка ПутьКОтчетуGenericExecution)
	_ПутьКОтчету        = ПутьКОтчетуGenericExecution;
КонецПроцедуры
