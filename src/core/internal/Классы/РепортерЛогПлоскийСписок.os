#Использовать collectionos

Перем _Лог;
Перем _РепортерСтатистика;
Перем _КодировкиUTF;

Процедура ВывестиТестПлан(ТестПлан) Экспорт

	ТестПлан.Дети()
		.ПроцессорКоллекции()
		.Развернуть("Набор -> Набор.Дети().ПроцессорКоллекции()")
		.ДляКаждого("Тест -> ВывестиТест(Тест)", ЭтотОбъект);

КонецПроцедуры

Процедура ВывестиТест(Тест) Экспорт

	Тест.Дети()
		.ДляКаждого("Тест -> ВывестиТест(Тест)", ЭтотОбъект);

	ИсполнениеТестКонец(Тест, Неопределено);

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестНаборПропущен")
Процедура ИсполнениеТестНаборПропущен(ТестНабор, Причина) Экспорт
	ВывестиВЛог(ТестНабор, Причина);
КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестКонец")
Процедура ИсполнениеТестКонец(Тест, Результат) Экспорт

	Если Тест.ТипОпределения() = ТипыОпределенийТестов.Тест Тогда
		ВывестиВЛог(Тест, Результат);
	КонецЕсли;

КонецПроцедуры

&ПодпискаНаСобытие("ИсполнениеТестПропущен")
Процедура ИсполнениеТестПропущен(Тест, Причина) Экспорт
	ВывестиВЛог(Тест, Причина);
КонецПроцедуры

Процедура ВывестиВЛог(Определение, РезультатВыполнения = Неопределено)

	Поля = Новый КартаСоответствие;
	Поля.Вставить("ИмяТеста",  Определение.ПолноеИмя());

	Уровень = УровниЛога.Информация;

	Если ТипЗнч(РезультатВыполнения) = Тип("РезультатВыполненияТеста") Тогда

		Если РезультатВыполнения.Результат() = РезультатыВыполненияТестов.Ошибка Тогда
			Уровень = УровниЛога.Ошибка;
		КонецЕсли;

		Причины = РезультатВыполнения.ОписанияПричин();
		Если Не Причины.Пусто() Тогда
			Поля.Вставить("Причина", Причины);
		КонецЕсли;

		Статистика = _РепортерСтатистика.СтатистикаТеста(Определение);

		Поля.Вставить("Результат", РезультатДляВывода(РезультатВыполнения.Результат()));
		Поля.Вставить(
			"ВремяВыполнения",
			 Статистика.ПолучитьИлиУмолчание("ВремяЗавершения", 0) -
			 Статистика.ПолучитьИлиУмолчание("ВремяНачала", 0)
		);

	ИначеЕсли ТипЗнч(РезультатВыполнения) = Тип("Строка") Тогда
		Уровень = УровниЛога.Предупреждение;
		Поля.Вставить("Причина", РезультатВыполнения);
		Поля.Вставить("Результат", РезультатДляВывода(Неопределено));
	Иначе
		// no-op
	КонецЕсли;

	_Лог.ПоляИз(Поля.КлючиИЗначения())
		.Вывести("", Уровень);

КонецПроцедуры

Функция РезультатДляВывода(Результат)

	Если _КодировкиUTF.Содержит(Консоль.КодировкаВходногоПотока) Тогда
		Успех   = "✔";
		Ошибка  = "✘";
		Пропуск = "↷";
	Иначе
		Успех   = "V";
		Ошибка  = "X";
		Пропуск = "О";
	КонецЕсли;

	Если Результат = РезультатыВыполненияТестов.Успех Тогда
		Возврат Успех;
	ИначеЕсли Результат = РезультатыВыполненияТестов.Ошибка Тогда
		Возврат Ошибка;
	Иначе
		Возврат Пропуск;
	КонецЕсли;

КонецФункции

&Желудь
Процедура ПриСозданииОбъекта(
	&Лог("oscript.lib.oneunit.core") Лог,
	&Пластилин РепортерСтатистика)

	_Лог = Лог;

	_РепортерСтатистика = РепортерСтатистика;

	_КодировкиUTF = Списки.ИзЭлементов(
		КодировкаТекста.UTF8,
		КодировкаТекста.UTF8NoBOM,
		КодировкаТекста.UTF16
	);

КонецПроцедуры
