#Использовать collectionos
#Использовать fs
#Использовать "../../../shared"

Перем _Поделка;
Перем _РежимВыводаЛога;

Перем _ПутьКОтчетуJUnit;
Перем _ПутьКОтчетуGenericExecution;
Перем _ПутьКОтчетуOpenTestReport;

Процедура ПриЗапускеПриложения() Экспорт

	_Поделка.НайтиЖелудь("РепортерСтатистика");

	Если ЗначениеЗаполнено(_ПутьКОтчетуJUnit) Тогда
		_Поделка.НайтиЖелудь("РепортерJUnit", Массивы.ИзЭлементов(_ПутьКОтчетуJUnit));
	КонецЕсли;

	Если ЗначениеЗаполнено(_ПутьКОтчетуGenericExecution) Тогда
		_Поделка.НайтиЖелудь("РепортерGenericExecution", Массивы.ИзЭлементов(_ПутьКОтчетуGenericExecution));
	КонецЕсли;

	Если ЗначениеЗаполнено(_ПутьКОтчетуOpenTestReport) Тогда
		_Поделка.НайтиЖелудь("РепортерOpenTestReport", Массивы.ИзЭлементов(_ПутьКОтчетуOpenTestReport));
	КонецЕсли;

	Если _РежимВыводаЛога = РежимыВыводаЛога.ПлоскийСписок Тогда
		_Поделка.НайтиЖелудь("РепортерЛогПлоскийСписок");
	ИначеЕсли _РежимВыводаЛога = РежимыВыводаЛога.Дерево Тогда
		_Поделка.НайтиЖелудь("РепортерЛогДерево");
	КонецЕсли; // BSLLS:IfElseIfEndsWithElse-off

КонецПроцедуры

&Рогатка
Процедура ПриСозданииОбъекта(
	&Пластилин Поделка,
	&Деталька("OneUnit.ПутьКОтчетуJUnit") ПутьКОтчетуJUnit,
	&Деталька("OneUnit.ПутьКОтчетуGenericExecution") ПутьКОтчетуGenericExecution,
	&Деталька("OneUnit.ПутьКОтчетуOpenTestReport") ПутьКОтчетуOpenTestReport,
	&Деталька("OneUnit.РежимВыводаЛога") РежимВыводаЛога)

	_Поделка = Поделка;

	_РежимВыводаЛога = РежимВыводаЛога;

	_ПутьКОтчетуJUnit            = ФС.ПолныйПуть(ПутьКОтчетуJUnit);
	_ПутьКОтчетуGenericExecution = ФС.ПолныйПуть(ПутьКОтчетуGenericExecution);
	_ПутьКОтчетуOpenTestReport   = ФС.ПолныйПуть(ПутьКОтчетуOpenTestReport);

КонецПроцедуры
