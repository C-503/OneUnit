#Использовать collectionos

Перем _Лог;

Процедура Ошибка(Знач Сообщение) Экспорт
	ОбработатьСтрокуЛога(Сообщение, Истина);
КонецПроцедуры

Процедура Информация(Знач Сообщение) Экспорт
	ОбработатьСтрокуЛога(Сообщение, Ложь);
КонецПроцедуры

Процедура ОбработатьСтрокуЛога(Сообщение, ЭтоОшибка)

	Сообщения = СтрРазделить(Сообщение, Символы.ПС);

	Для Каждого Сообщение Из Сообщения Цикл

		Сообщение = СтрЗаменить(Сообщение, Символы.ВК, "");
		// В win при переключении консоли в UTF-8 появляется символ с кодом 65279 в начале сообщения и парсер JSON падает
		Сообщение = СтрЗаменить(Сообщение, Символ(65279), "");

		Попытка

			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.УстановитьСтроку(Сообщение);
			Поля = ПрочитатьJSON(ЧтениеJSON, Истина, Массивы.ИзЭлементов("time"));
			ТекстСообщения = Поля.Получить("msg");

			Уровень = Поля.Получить("level");

			Если Уровень = "DEBUG" Тогда
				Уровень = УровниЛога.Отладка;
			ИначеЕсли Уровень = "INFO" Тогда
				Уровень = УровниЛога.Информация;
			ИначеЕсли Уровень = "WARN" Тогда
				Уровень = УровниЛога.Предупреждение;
			ИначеЕсли Уровень = "ERROR" Тогда
				Уровень = УровниЛога.Ошибка;
			ИначеЕсли Уровень = "FATAL" Тогда
				Уровень = УровниЛога.КритичнаяОшибка;
			КонецЕсли; // BSLLS:IfElseIfEndsWithElse-off

			_Лог.ПоляИз(Поля)
				.Вывести(ТекстСообщения, Уровень);

		Исключение

			Если ЭтоОшибка Тогда
				_Лог.Ошибка(Сообщение);
			Иначе
				_Лог.Информация(Сообщение);
			КонецЕсли;

		КонецПопытки;

	КонецЦикла;

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(&Лог("oscript.lib.oneunit.cli") Лог)
	_Лог = Лог;
КонецПроцедуры
